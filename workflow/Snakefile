## Snakemake Climaso (The CLimate IMpact of AeroSOls: forcing and feedbacks)
##
## Ove Haugvaldstad
##
## MIT license.
##
import os
import glob
import yaml
from scripts.create_lookup_tables import (generate_experiment_lookup_table,
                                    generate_institu_lookup_table,
                                    generate_file_ending_lookup_table)
import socket

configfile: 'config/config.yaml'

hostname = socket.gethostname()
if 'betzy' in hostname:
    ROOT_PATH = '/cluster/shared/ESGF'
else:
    ROOT_PATH = config['root']
ROOT_PATH_NORESM = config['root_noresm']
CMIP_VER = config['cmip_version']
VARS=config['variables']
EXPERIMENTS=config['experiments']['experiments']
TABLE_IDS=config['table_ids']
DEFAULT_TABLE_ID=config['table_id_default']

CONTROL_EXPS=config['experiments']['control']
ALL_ALLOWED = EXPERIMENTS + CONTROL_EXPS

try:
    with open('config/lookup_experiments.yaml', 'r') as f:
        LOOK_EXP=yaml.safe_load(f)
except FileNotFoundError:
    LOOK_EXP = generate_experiment_lookup_table(ROOT_PATH, CMIP_VER)
    with open('config/lookup_experiments.yaml', 'w') as f:
        LOOK_EXP=yaml.dump(LOOK_EXP,f,default_flow_style=False)

try:
    with open('config/institution.yaml', 'r') as f:
        LOOK_INSTITU=yaml.safe_load(f)
except FileNotFoundError:
    LOOK_INSTITU = generate_institu_lookup_table(ROOT_PATH, CMIP_VER)
    with open('config/institution.yaml', 'w') as f:
        LOOK_INSTITU=yaml.dump(LOOK_INSTITU,f,default_flow_style=False)
try:
    with open(config['lookup_file_endings'], 'r') as f:
        LOOK_FNAMES=yaml.safe_load(f)

except FileNotFoundError:
    LOOK_FNAMES=generate_file_ending_lookup_table(ROOT_PATH,ALL_ALLOWED,CMIP_VER)
    with open('config/lookup_file_endings.yaml', 'w') as f:
        yaml.dump(LOOK_FNAMES,f,default_flow_style=False)

try:
    if config.get('lookup_file_endings_NorESM', None):
        with open(config['lookup_file_endings_NorESM'], 'r') as f:
            LOOK_FNAMES_NORESM = yaml.safe_load(f)
except FileNotFoundError:
    LOOK_FNAMES_NORESM=generate_file_ending_lookup_table(ROOT_PATH_NORESM,ALL_ALLOWED,CMIP_VER)
    with open(config['lookup_file_endings_NorESM'], 'w') as f:
        yaml.dump(LOOK_FNAMES_NORESM,f,default_flow_style=False)


def get_paths(w, variable,experiment, grid_label=None, activity=None, control=False):
    """
    Get CMIP6 model paths in database based on the lookup tables.

    Parameters:
    -----------
        w : snake.wildcards
                a named tuple that contains the snakemake wildcards

    """
    model = w.model
    if model in ["NorESM2-LM", "NorESM2-MM"]:
        root_path = f'{ROOT_PATH_NORESM}/{CMIP_VER}'
        look_fnames = LOOK_FNAMES_NORESM
    else:
        root_path = f'{ROOT_PATH}/{CMIP_VER}'
        look_fnames = LOOK_FNAMES
    if activity:
        activity= activity
    else:
        activity = LOOK_EXP[experiment]
        
        if experiment not in list(look_fnames[activity][model].keys()):
            activity = config.get('default_activity','CMIP')
        
    if control:
        prefered_variant=config['model_specific_variant']['control'].get(model, config['variant_default'])
    else:
        prefered_variant = config['model_specific_variant']['experiment'].get(model, config['variant_default'])
    table_id = TABLE_IDS.get(variable,DEFAULT_TABLE_ID)
    institution = LOOK_INSTITU[model]
    try:
        file_endings = look_fnames[activity][model][experiment]
    except:
        raise KeyError(f"File ending is not defined for this combination of {activity}, {model} and {experiment} " +
                        "please update config/lookup_file_endings.yaml accordingly")
    available_variants = list(file_endings.keys())
    if prefered_variant in available_variants:
        variant = prefered_variant
    else:
        print("did not find prefered variant {} in [{}]".format(prefered_variant,' '.join(available_variants)))
        print("selecting variant {}".format(available_variants[0]))
        variant = available_variants[0]
    if grid_label == None:
        grid_label = look_fnames[activity][model][experiment][variant][table_id]['gl'][0]
    check_path = f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}'
    if os.path.exists(check_path)==False:
        grid_labels = ['gr','gn', 'gl','grz', 'gr1']
        i = 0
        while os.path.exists(check_path)==False and i < len(grid_labels):
            grid_label = grid_labels[i]
            check_path = f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}'
            i += 1 
    if control:
        version = config['version']['version_control'].get(w.model, 'latest')
    else:
        version = config['version']['version_exp'].get(experiment, 'latest')
        if version != 'latest':
            version = version.get(w.model, 'latest')
    fname = f'{variable}_{table_id}_{model}_{experiment}_{variant}_{grid_label}'
    if os.path.exists(check_path):
        fn = glob_wildcards(
            f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}/{version}/{fname}_{{file_endings}}')
        paths = expand(
            f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}/{version}/{fname}_{{file_endings}}'
            ,file_endings=fn.file_endings)
        paths = sorted(paths)
    else:
        raise FileNotFoundError(f"{check_path} does not exist")
        import sys
        sys.exit()
    return paths

def get_control_path(w, variable, grid_label=None):
    if grid_label == None:
        grid_label = config['default_grid_label']
    if w.get('experiment', None) == 'piControl':
        paths = get_paths(w, variable,'piControl', grid_label,activity='CMIP', control=False)
    elif w.get('experiment',None) == 'histSST':
        paths = get_paths(w, variable, 'histSST-piAer', grid_label, activity='AerChemMIP',control=False)
    else:
        try:
            paths = get_paths(w, variable,'piClim-control', grid_label,activity='RFMIP', control=True)
        except KeyError:
            paths = get_paths(w, variable,'piClim-control', grid_label,activity='AerChemMIP', control=True)
    if not paths:
        print(paths)
        import sys; sys.exit()
    return paths

output_format = {}



# set root outdir path
if config['out_dir']:
    outdir = config['out_dir']
else:
    outdir = 'results/'

if config['output_format']=='aerocom_column':
    output_format['single_variable'] = outdir+'{model}_{experiment}/renamed/aerocom3_{model}_{experiment}_{variable}_Column_{freq}_monthly.nc'
    print(output_format)
    valid_frequencies = 'daily|annual|2010|monthly'
else:
    output_format['single_variable'] = outdir+'{experiment}/{variable}/{variable}_{experiment}_{model}_{freq}.nc'
    valid_frequencies = 'Aday|Amon|clim|Ayear'

if config['no_intermediate_files']:
    output_format = {key:temp(item) for key, item in output_format.items()}

wildcard_constraints:
    freq=valid_frequencies

include: 'rules/compute_ERF.smk'
include: 'rules/utils.smk'
include: 'rules/calc_feedback.smk'
include: 'rules/diagnose_cloud_changes.smk'
include: 'rules/plotAerChemMIP.smk'



rule all:
    input:
        expand(outdir+'figs/AerChemMIP/ERFs/{vName}_piClim-2xdust_AerChemMIP.png',
                        vName=['ERFtsw','ERFtlw','ERFtcs','ERFt']),
        expand(outdir+'figs/AerChemMIP/ERFs/{vName}_piClim-2xdust_globalaverage_AerChemMIP.png',
                        vName=['ERFtsw','ERFtlw','ERFtcs','ERFt']),
        expand(outdir+'figs/AerChemMIP/ERFs/{vName}_piClim-2xdust_AerChemMIP.png',
                        vName=['SWDirectEff','LWDirectEff', 'SWCloudEff','LWCloudEff','LWDirectEff_cs','SWDirectEff_cs','CloudEff','DirectEff']),
        # outdir+'figs/AerChemMIP/delta_2xdust/cdnc_piClim-2xdust_AerChemMIP.png'




rule clim_od550dust_aerocom_CMIP6:
    input:
        expand(outdir+'{model}_{experiment}/renamed/aerocom3_{model}_{experiment}_{variable}_Column_2010_monthly.nc',
                    variable='od550dust',experiment=['piClim-control','piClim-2xdust'],
                    model=['EC-Earth3-AerChem', 'GISS-E2-1-G', 'IPSL-CM6A-LR-INCA', 
                        'UKESM1-0-LL', 'GFDL-ESM4', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1', 'NorESM2-LM'])

rule clim_od550aer_aerocom_CMIP6:
    input:
        expand(outdir+'{model}_{experiment}/renamed/aerocom3_{model}_{experiment}_{variable}_Column_2010_monthly.nc',
                    variable='od550aer',experiment=['piClim-control','piClim-2xdust'],
                    model=['GISS-E2-1-G',  
                         'GFDL-ESM4',
                        'CNRM-ESM2-1'])          
