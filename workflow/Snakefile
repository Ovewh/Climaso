## Snakemake Climaso (The CLimate IMpact of AeroSOls: forcing and feedbacks)
##
## Ove Haugvaldstad
##
## MIT license.
##
import os
import glob
import yaml
from scripts.create_lookup_tables import (generate_experiment_lookup_table,
                                    generate_institu_lookup_table,
                                    generate_file_ending_lookup_table)
import socket

configfile: 'config/config.yaml'

hostname = socket.gethostname()

if hostname.split('.')[1] == 'betzy':
    ROOT_PATH = '/cluster/shared/ESGF'
else:
    ROOT_PATH = config['root']
ROOT_PATH_NORESM = config['root_noresm']
CMIP_VER = config['cmip_version']
VARS=config['variables']
EXPERIMENTS=config['experiments']['experiments']
TABLE_IDS=config['table_ids']
DEFAULT_TABLE_ID=config['table_id_default']

CONTROL_EXPS=config['experiments']['control']
ALL_ALLOWED = EXPERIMENTS + CONTROL_EXPS

try:
    with open('config/lookup_experiments.yaml', 'r') as f:
        LOOK_EXP=yaml.safe_load(f)
except FileNotFoundError:
    LOOK_EXP = generate_experiment_lookup_table(ROOT_PATH, CMIP_VER)
    with open('config/lookup_experiments.yaml', 'w') as f:
        LOOK_EXP=yaml.dump(LOOK_EXP,f,default_flow_style=False)

try:
    with open('config/institution.yaml', 'r') as f:
        LOOK_INSTITU=yaml.safe_load(f)
except FileNotFoundError:
    LOOK_INSTITU = generate_institu_lookup_table(ROOT_PATH, CMIP_VER)
    with open('config/institution.yaml', 'w') as f:
        LOOK_INSTITU=yaml.dump(LOOK_INSTITU,f,default_flow_style=False)
try:
    with open(config['lookup_file_endings'], 'r') as f:
        LOOK_FNAMES=yaml.safe_load(f)
    if config.get('lookup_file_endings_NorESM', None):
        with open(config['lookup_file_endings_NorESM'], 'r') as f:
            LOOK_FNAMES_NORESM = yaml.safe_load(f)

except FileNotFoundError:
    LOOK_FNAMES=generate_file_ending_lookup_table(ROOT_PATH,ALL_ALLOWED,CMIP_VER)
    with open('config/lookup_file_endings.yaml', 'w') as f:
        LOOK_FNAMES=yaml.dump(LOOK_FNAMES,f,default_flow_style=False)
    


def get_paths(w, variable,experiment, grid_label=None, activity=None, control=False):
    """
    Get CMIP6 model paths in database based on the lookup tables.

    Parameters:
    -----------
        w : snake.wildcards
                a named tuple that contains the snakemake wildcards

    """
    if w.model in ["NorESM2-LM", "NorESM2-MM"]:
        root_path = f'{ROOT_PATH_NORESM}/{CMIP_VER}'
        look_fnames = LOOK_FNAMES_NORESM
    else:
        root_path = f'{ROOT_PATH}/{CMIP_VER}'
        look_fnames = LOOK_FNAMES
    if activity:
        activity= activity
    else:
        activity = LOOK_EXP[experiment]
    model = w.model
    if control:
        variant=config['model_specific_variant']['control'].get(model, config['variant_default'])
    else:
        variant = config['model_specific_variant']['experiment'].get(model, config['variant_default'])
    table_id = TABLE_IDS.get(variable,DEFAULT_TABLE_ID)
    institution = LOOK_INSTITU[model]
    try:
        file_endings = look_fnames[activity][model][experiment][variant][table_id]['fn']
    except:
        raise KeyError(f"File ending is not defined for this combination of {activity}, {model}, {experiment}, {variant} and {table_id} " +
                        "please update config/lookup_file_endings.yaml accordingly")
    if grid_label == None:
        grid_label = look_fnames[activity][model][experiment][variant][table_id]['gl'][0]
    check_path = f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}'
    if os.path.exists(check_path)==False:
        grid_labels = ['gr','gn', 'gl','grz', 'gr1']
        i = 0
        while os.path.exists(check_path)==False and i < len(grid_labels):
            grid_label = grid_labels[i]
            check_path = f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}'
            i += 1 
    if control:
        version = config['version']['version_control'].get(w.model, 'latest')
    else:
        version = config['version']['version_exp'].get(w.model, 'latest')
    fname = f'{variable}_{table_id}_{model}_{experiment}_{variant}_{grid_label}'
    paths = expand(
        f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}/{version}/{fname}_{{file_endings}}'
        ,file_endings=file_endings)
    # Sometimes the verisons are just messed up... try one more time with latest
    if not os.path.exists(paths[0]):
        paths=expand(
        f'{root_path}/{activity}/{institution}/{model}/{experiment}/{variant}/{table_id}/{variable}/{grid_label}/latest/{fname}_{{file_endings}}'
        ,file_endings=file_endings)
    return paths

def get_control_path(w, variable, grid_label=None):
    if grid_label == None:
        grid_label = config['default_grid_label']
    try:
        paths = get_paths(w, variable,'piClim-control', grid_label,activity='RFMIP', control=True)
    except KeyError:
        paths = get_paths(w, variable,'piClim-control', grid_label,activity='AerChemMIP', control=True)
    return paths

output_format = {}



# set root outdir path
if config['out_dir']:
    outdir = config['out_dir']
else:
    outdir = 'results/'

if config['output_format']=='aerocom_column':
    output_format['single_variable'] = outdir+'{model}/renamed/aerocom3_{model}_{experiment}_{variable}_Column_{freq}_monthly.nc'
    valid_frequencies = 'daily|annual|9999|monthly'
else:
    output_format['single_variable'] = outdir+'{experiment}/{variable}/{variable}_{experiment}_{model}_{freq}.nc'
    valid_frequencies = 'Aday|Amon|clim|Ayear'

if config['no_intermediate_files']:
    output_format = {key:temp(item) for key, item in output_format.items()}

wildcard_constraints:
    freq=valid_frequencies

include: 'rules/compute_ERF.smk'
include: 'rules/calculate_single_variable_climatologies.smk'
include: 'rules/plotAerChemMIP.smk'


## piClim2xdustERFbetzy     : Calculates ERF(LW/SW/Total) and the atmospheric absorbtion 
##                            between piClim-control and piClim-2xdust (works on betzy)
rule piClim2xdustERFbetzy:
    input:
        expand('results/piClim-2xdust/atm_abs/atmabs_piClim-2xdust_{model}_Ayear.nc', 
                model=['EC-Earth3-AerChem', 'GISS-E2-1-G', 'IPSL-CM6A-LR-INCA', 
                        'MIROC6','UKESM1-0-LL', 'GFDL-ESM4', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1']),

        expand('results/piClim-2xdust/{vName}/{vName}_piClim-2xdust_{model}_Ayear.nc', 
                model=['EC-Earth3-AerChem', 'GISS-E2-1-G', 'IPSL-CM6A-LR-INCA', 
                        'MIROC6','UKESM1-0-LL', 'GFDL-ESM4', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1'], vName = ['ERFtsw','ERFtlw','ERFtcs'])
## piClim2xdustRadbetzy     : Calculates the direct and cloud radiative effects between piClim-control
##                            and piClim-2xdust (works on betzy)
rule piClim2xdustRadbetzy:
    input:
        expand('results/piClim-2xdust/cloud_rad/{vName}_piClim-2xdust_{model}_Ayear.nc',
            vName=['CloudEff','SWCloudEff','LWCloudEff'], 
            model=['EC-Earth3-AerChem',  
                        'UKESM1-0-LL', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1','NorESM2-LM']),
        expand('results/piClim-2xdust/rad/{vName}_piClim-2xdust_{model}_Ayear.nc',
            vName=['SWDirectEff','SWDirectEff_cs','LWDirectEff','LWDirectEff_cs'], 
            model=['EC-Earth3-AerChem', 
                        'UKESM1-0-LL', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1', 'NorESM2-LM'])

rule norESM:
    input:
        'results/piClim-2xdust/atm_abs/atmabs_piClim-2xdust_NorESM2-LM_Ayear.nc',
        expand('results/piClim-2xdust/rad/{vName}_piClim-2x_NorESM2-LM_Ayear.nc',
                vName=['SWDirectEff','SWDirectEff_cs','LWDirectEff','LWDirectEff_cs']),
        expand('results/piClim-2xdust/cloud_rad/{vName}_piClim-2xdust_NorESM2-LM_Ayear,nc',
               vName=['CloudEff','SWCloudEff','LWCloudEff']),
        expand('results/piClim-2xdust/{vName}/{vName}_piClim-2xdust_NorESM2-LM_Ayear.nc', 
                vName = ['ERFtsw','ERFtlw','ERFtcs']) 

rule clim_od550dust_aerocom_CMIP6:
    input:
        expand(outdir+'{model}/renamed/aerocom3_{model}_{experiment}_{variable}_Column_9999_monthly.nc',
                    variable='od550dust',experiment=['piClim-control'],
                    model=['EC-Earth3-AerChem', 'GISS-E2-1-G', 'IPSL-CM6A-LR-INCA', 
                        'UKESM1-0-LL', 'GFDL-ESM4', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1'])

rule clim_od550aer_aerocom_CMIP6:
    input:
        expand(outdir+'{model}/renamed/aerocom3_{model}_{experiment}_{variable}_Column_9999_monthly.nc',
                    variable='od550aer',experiment=['piClim-control'],
                    model=['EC-Earth3-AerChem', 'GISS-E2-1-G', 'IPSL-CM6A-LR-INCA', 
                        'UKESM1-0-LL', 'GFDL-ESM4', 'MPI-ESM-1-2-HAM',
                        'CNRM-ESM2-1'])          
